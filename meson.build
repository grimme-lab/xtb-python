# This file is part of xtb.
#
# Copyright (C) 2020 Sebastian Ehlert
#
# xtb is free software: you can redistribute it and/or modify it under
# the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# xtb is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with xtb.  If not, see <https://www.gnu.org/licenses/>.

project(
  'xtb-python',
  'c',
  license: 'LGPL3',
)

cc = meson.get_compiler('c')

# Import xtb as subproject, we need the API of version 6.3 or newer,
# also we disable the internal ctypes Python API of xtb since we provide our own
# and force xtb to provide a _static_ library to avoid depending on xtb at runtime
xtb_prj = subproject(
  'xtb',
  version: '>=6.3',
  default_options: [
    'default_library=static',
    'static=false',
    'openmp=@0@'.format(get_option('openmp')),
    'la_backend=@0@'.format(get_option('la_backend')),
  ],
)
xtb_dep = xtb_prj.get_variable('xtb_dep')
xtb_header = xtb_prj.get_variable('xtb_header')

pymod = import('python')
python = pymod.find_installation(
  'python@0@'.format(get_option('py')),
  modules: [
    'cffi',
    'numpy',
  ],
)
python_dep = python.dependency(required: true)

# Python's CFFI is horrible in working with preprocessor statements,
# therefore, we have to preprocess the header before passing it to the ffibuilder
xtb_pp = configure_file(
  command: [cc, '-E', '@INPUT@'],
  input: xtb_header,
  output: '_libxtb.h',
  capture: true,
)

# This is the actual out-of-line API processing of the ffibuilder
xtb_cffi_srcs = configure_file(
  command: [python, files('ffibuilder.py'), '@INPUT@', '@BASENAME@'],
  input: xtb_pp,
  output: '@BASENAME@.c',
)

# Actual generation of the Python extension, since the shared_module does not work
# well with dependency objects, we will trick it by linking a whole static lib
xtb_pyext = python.extension_module(
  '_libxtb',
  link_whole: static_library(
    '_libxtb',
    xtb_cffi_srcs,
    dependencies: [xtb_dep, python_dep],
  ),
  dependencies: [xtb_dep, python_dep],
  install: true,
)
